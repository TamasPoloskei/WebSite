!* FEA computer laboratory H2 problem
!*
!* EXCENTRICALLY LOADED U-SECTION BEAM
!*
!* created by Tamas Poloskei
!* 2017. February 22.
!*
!* covert everything to a consistent unit system
!* [N],[Nmm],[N/mm^2=MPa],[N/mm]
!*

/COM,  Structural   
!*  
/PREP7  
!*
/VIEW,1,1,1,1   
/ANG,1  
/REP,FAST   
!*
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
!*
ELAST=2E5  
nu=0.3 
rho=7.8e-9 
t=10   
F=3000 
!*
!*  set element type
ET,1,SHELL181   
!*
!* define the material properties
!* elasticity
!* Temperatures must be defined first
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,nu
!* density
!* Temperatures must be defined first
MPTEMP
MPTEMP,1,0  
MPDATA,DENS,1,,rho  
!*
!* define the layup of the shell
sect,1,shell,,  
secdata, t,1,0.0,3  
secoffset,MID   
seccontrol
!* 
!* defining the keypoints
K,1,0,0,0,   
K,2,0,0,1000,   
K,3,100,0,0,
K,4,0,200,0,
K,5,100,200,0,  
!*
!* creating the lines
!* U section
L,5,4
L,4,1
L,1,3
!*
!* line along the beam
L,1,2
!*
!* create the model by extruding the contour of the U-section
ADRAG,1,2,3,,,,4
!*
!* gluing the extruded areas
AGLUE,ALL
!*
!* setting the number of elements on the lines
LESIZE,1,,,10
LESIZE,2,,,20
LESIZE,3,,,10
LESIZE,5,,,10
LESIZE,6,,,60   
LESIZE,7,,,60 
LESIZE,8,,,20
LESIZE,9,,,60
LESIZE,10,,,10
!*
!* mesh the areas using mapped meshing algorithm
MSHKEY,1
AMESH,ALL
!*
!* reset mesh option
MSHKEY,0
!*
!* select nodes at z = 0
NSEL,S,LOC,Z,0,0
!*
!* constrain all DoFs at this location
D,ALL,ALL ,0   
!*
!* select everything
ALLSEL,ALL
!* select nodes at x = 0, z = 1000
NSEL,S,LOC,X,0,0
NSEL,R,LOC,Z,1000,1000  
!*
!* apply the calculated force at every node at this location
F,ALL,FY,-F/21 
!*
!* select everythinf
ALLSEL,ALL
FINISH
!*
!* solve the finite element equation K.U = F
/SOL
SOLVE
!*
!* post processing
/POST1
!*
!* printing the displaced structure
PLNSOL, U,X, 0,1.0  
/IMAGE,SAVE,disp_x,bmp
PLNSOL, U,Y, 0,1.0  
/IMAGE,SAVE,disp_y,bmp
PLNSOL, U,Z, 0,1.0  
/IMAGE,SAVE,disp_z,bmp
!*
!* plotting the stress values at the longitudinal edge of the beam
!* x = 0, y = 0
PATH,2,2,30,100
PPATH,1,,0,0,0
PPATH,2,,0,0,1000
PDEF,SX,S,X,AVG 
PDEF,SY,S,Y,AVG 
PDEF,SZ,S,Z,AVG 

PLPATH,SX 
/IMAGE,SAVE,sx,bmp

PLPATH,SY 
/IMAGE,SAVE,sy,bmp

PLPATH,SZ 
/IMAGE,SAVE,sz,bmp

FINISH
/SOL
!*
!* modal analysis, first 10 natural frequencies
ANTYPE,2
!*  
MODOPT,LANB,10  
EQSLV,SPAR  
MXPAND,10, , ,1 
LUMPM,0 
PSTRES,0
!*  
MODOPT,LANB,10,0,0, ,OFF
SOLVE   
FINISH  
/POST1  
SET,FIRST     
!*
!* printing the eigenshapes
*DO, III, 1, 10,1
	PLNSOL, U,Y, 2,1.0   
	/IMAGE,SAVE,strcat(chrval(III),'_eigenshape'),bmp
	SET,NEXT
*ENDDO