!* FEA computer laboratory NL4 problem
!*
!* TERMOMECHANICAL ANALYSIS OF A PIPE-TANK ASSEMBLY
!*
!* created by Tamas Poloskei
!* 2017. April 18.
!*
!* covert everything to a consistent unit system
!* [N],[NM],[N/M^2=Pa],[N/m]
!*
/PREP7
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
!*
RI1 = 0.03302
RO1 = 0.0381
RI2 = 0.01016
RO2 = 0.0127
Z2 = 0.0508
ELAST = 200E9
NU = 0.3
RHO = 7890
ALPHA = 1.435E-5
Tt = 505.37
Tp = 310.92
LAMBDA = 1419.56
!*
/COM,  Thermal
!*
ET,1,SOLID278
!*
!* density
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,DENS,1,,RHO 
!*
!* conductivity
MPTEMP,,,,,,,,  
MPTEMP,1,294.26 
MPTEMP,2,366.48 
MPTEMP,3,422.03 
MPTEMP,4,477.59 
MPTEMP,5,533.15 
MPDATA,KXX,1,,14.45 
MPDATA,KXX,1,,15.40 
MPDATA,KXX,1,,16.18
MPDATA,KXX,1,,16.96 
MPDATA,KXX,1,,17.91
!*
!* specific heat
MPTEMP,,,,,,,,  
MPTEMP,1,294.36 
MPTEMP,2,366.48 
MPTEMP,3,422.03 
MPTEMP,4,477.59 
MPTEMP,5,533.15 
MPDATA,C,1,,473.11  
MPDATA,C,1,,489.56  
MPDATA,C,1,,498.22  
MPDATA,C,1,,510.79  
MPDATA,C,1,,523.35
!*
!* covection or film coefficient
MPTEMP,,,,,,,,  
MPTEMP,1,294.36 
MPTEMP,2,366.48 
MPTEMP,3,422.03 
MPTEMP,4,477.59 
MPTEMP,5,533.15 
MPDATA,HF,2,,2418.94
MPDATA,HF,2,,2299.7 
MPDATA,HF,2,,1998.75
MPDATA,HF,2,,1561.52
MPDATA,HF,2,,1254.9
!*
CYLIND,RO1,RI1,0,Z2,0,90
!*
!* rotate workplane
wprot,0,-90,0 
!*
CYLIND,RO2,RI2,0,Z2,0,-90
!*
wprot,0,90,0
!*
!* overlap volumes
VOVLAP,1,2
!*
!* delete unnecessary volumes
VDELE,3,,,1
VDELE,4,,,1
!*
!* mapped meshing algorithm
MSHKEY,1
!*  
MSHAPE,0,2D 
MSHAPE,0,3D
!*
!* concatenate areas
ACCAT,2,5
!*
!* concatenate lines
LCCAT,5,10
LCCAT,7,12
!*
!* element number definition along lines
LESIZE,7,,,12
LESIZE,12,,,10
LESIZE,18,4
LESIZE,39,,,22
LESIZE,47,,,4
!*
!* mesh volumes
VMESH,ALL
FINISH  
/SOL
TUNIF,Tt
!*
CSYS,1
NSEL,S,LOC,X,RI1
SF,ALL,CONV,LAMBDA,Tt
!*
NSEL,S,LOC,Z,Z2
D,ALL,TEMP,Tt
ALLSEL,ALL
!*
!* create cylindrical coordinatesystem
!*
wprot,0,-90,0
!*
CSWPLA,11,1,1,1,
!*
!* set convection on inner part of the pipe
ALLSEL,ALL
NSEL,S,LOC,X,RI2
SF,ALL,CONV,-2,Tp
ALLSEL,ALL
!*
AUTOTS,1
NSUBST,50, , ,1
SOLVE
!*
/COM,  Structural
!*
!* change element type from thermal to structural
/PREP7
ETCHG,TTS
!*
!* define material properties
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU   
MPTEMP,,,,,,,,  
MPTEMP,1,0  
UIMP,1,REFT,,,273+20
MPDATA,ALPX,1,,ALPHA
!*
!* symmetry constraints
DA,24,UZ,0
DA,28,UZ,0
DA,31,UZ,0
!*
DA,26,UX,0
DA,29,UX,0
DA,32,UX,0
!*
DA,5,UY,0
FINISH  
/SOL
!*  
ANTYPE,0
LDREAD,TEMP,,,,,,rth
SOLVE   
FINISH