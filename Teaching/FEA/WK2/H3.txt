!* FEA computer laboratory H1 problem
!*
!* STATIC ANALYSIS OF A TUBE COMPENSATOR
!*
!* created by Tamas Poloskei
!* 2017. February 14.
!*
!* covert everything to a consistent unit system
!* [N],[Nmm],[N/mm^2=MPa],[N/mm]
!*
elast = 2E5
nu = 0.3
py = -0.001
rho = 8E-9

/COM,  Structural
!*
/PREP7  
!*  
!* element type definition
ET,1,SHELL181   
!*  
ET,2,BEAM189
!*  
!* define the material properties
!* elasticity
!* Temperatures must be defined first
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,elast  
MPDATA,PRXY,1,,nu 
!* density
!* Temperatures must be defined first
MPTEMP  
MPTEMP,1,0  
MPDATA,DENS,1,,rho
!*
!* shell lay-up
sect,1,shell,,  
secdata, 5,1,0.0,3  
secoffset,MID   
!*
!* beam section
SECTYPE,   2, BEAM, RECT, BEAM3D, 0 
SECOFFSET, USER, 0, 12.5
SECDATA,5,20
!*
!* create keypoints
K,1,0,0
K,2,1000,0
!*
!* create straight line
L,1,2
!*
!* copy lines
LGEN,2,1,,,,,250
LGEN,2,2,,,,,500 
LGEN,2,3,,,,,500 
LGEN,2,4,,,,,250 

TYPE,2
SECN,2
!*
!* copy keypoints 3,5,7
KGEN,2,3,7,2,,100
!*
!* element number definition along lines
LESIZE,2,,,36
LESIZE,3,,,36
LESIZE,4,,,36
!*
!* select L2 and set mesh attributes
LSEL,S,LINE,,2 
LATT,1,,2,,11,,2
!* select L3 and set mesh attributes
LSEL,S,LINE,,3 
LATT,1,,2,,12,,2
!* select L4 and set mesh attributes
LSEL,S,LINE,,4
LATT,1,,2,,13,,2

ALLSEL,ALL  
L,1,3
L,2,4
L,3,5
L,4,6
L,5,7
L,6,8
L,7,9
L,8,10
!*
!*
AL,1,6,2,7
AL,2,8,3,9
AL,3,10,4,11
AL,4,12,5,13
!*
!* setting the number of finite elements on lines along axis Z
LESIZE,6,,,8
LESIZE,8,,,16
LESIZE,10,,,16
LESIZE,12,,,8
!*
!* meshing the beam

LMESH,2,4,1 
!* meshing the areas with shell elements
TYPE,1
SECN,1
MSHKEY,1
AMESH,ALL
!*
!* kinematic constraints
DL,6,,ALL,0 
DL,8,,ALL,0 
DL,10,,ALL,0
DL,12,,ALL,0
!*
!* external load
SFA,ALL,2,PRES,py  
FINISH  
/SOL
SOLVE   
FINISH  
/POST1 
!*
!* top view
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
!*
!* top view
/VIEW,1,,1  
/ANG,1  
/REP,FAST   
!* printing images
PLNSOL, U,Y, 0,1.0  
/IMAGE,SAVE,u_y,bmp
PLNSOL, U,X, 0,1.0  
/IMAGE,SAVE,u_x,bmp
PLNSOL, U,Z, 0,1.0  
/IMAGE,SAVE,u_z,bmp
!*
PLNSOL, S,Z, 0,1.0  
/IMAGE,SAVE,s_z,bmp
PLNSOL, S,XZ, 0,1.0 
/IMAGE,SAVE,t_xz,bmp
FINISH  
/SOL
!*  
!* modal analysis
ANTYPE,2
!*  
!*  first 10 eigenfrequencies
MODOPT,LANB,10  
EQSLV,SPAR  
MXPAND,10, , ,1 
LUMPM,0 
PSTRES,0
!*  
MODOPT,LANB,10,0,0, ,OFF
SOLVE   
FINISH  
/POST1  
SET,FIRST   
!*
!* printing the eigenshapes
*DO, III, 1, 10,1
	PLNSOL, U,Y, 0,1.0  
	/IMAGE,SAVE,strcat(chrval(III),'_eigen_shape'),bmp
	SET,NEXT
*ENDDO