!* FEA computer laboratory H1 problem
!*
!* LARGE DEFORMATION OF A BUILT-IN PLATE - GEOMETRIC NONLINEARITY
!*
!* created by Tamas Poloskei
!* 2017. March 07.
!*
!* convert everything to a consistent unit system
!* [N],[Nm],[N/m^2=Pa],[N/m]
!*
/COM,  Structural
!*
a = 40
b = 30
t = 0.4
F = 40000
ELAST = 120E6
NU = 0.3
/PREP7
!*
/VIEW,1,1,1,1   
/ANG,1  
/REP,FAST 
!*
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
!*
!* set element type
ET,1,SHELL181   
!*  
!* linear elastic material model
MPTEMP 
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU  
!*
!* thickness definition
sect,1,shell,,  
secdata, T,1,0.0,3 
secoffset,MID   
seccontrol
!*
!*  create keypoints
K,1,0,0,0
K,2,a,0,0
K,3,a,b,0
K,4,0,b,0
!*
!* create area using keypoints
A,1,2,3,4
!*
!* set division number on lines
LESIZE,ALL,,,20 
!*
!* mesh areas as mapped
MSHKEY,1
AMESH,ALL
FINISH  
/SOL
!*  
!* new static analysis
ANTYPE,0
NLGEOM,1
OUTRES,ALL,ALL  
NEQIT,150   

DL,4,,ALL,0 
FK,2,FZ,-F/5
LSWRITE,1
FK,2,FZ,-2*F/5
LSWRITE,2
FK,2,FZ,-3*F/5
LSWRITE,3
FK,2,FZ,-4*F/5
LSWRITE,4
FK,2,FZ,-5*F/5
LSWRITE,5
LSSOLVE,1,5,1
/INPUT,'H1_macro','txt','C:\Users\TAMAS\Google Drive\BME\PhD\2016.17.02\OKTATAS\FEA\labor\',, 0