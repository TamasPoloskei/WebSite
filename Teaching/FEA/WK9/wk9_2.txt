!* FEA computer laboratory D5 problem
!*
!* DYNAMIC ANALYSIS OF A 4_BAR LINKAGE
!*
!* created by Tamas Poloskei
!* 2017. April 04.
!*
!* covert everything to a consistent unit system
!* [N],[Nc,],[N/cm^2],[N/cm]

/COM,  Structural   
!*
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
!*
!* define parameters
ELAST = 10E6
nu = 0.3
AREA = 0.063
Iz = 0.00525
Iy = 2.08373E-5
rho = 2.588E-4
TKZ = 0.063
!*
D1 = 0.000139
D2 = 0.000278
!*
K1 = 10E6
K2 = 10E6
K3 = 10E6
K4 = 1.5528E-4
!*
/PREP7 
ET,1,BEAM4

ET,2,COMBIN7
!*  

R,1,AREA,Iz,Iy,TKZ
R,2,K1,K2,K3,K4
!*
!* create materials
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU   
MPTEMP 
MPTEMP,1,0  
MPDATA,DENS,1,,rho  
MPTEMP 
MPTEMP,1,0  
MPDATA,DMPR,1,,D1   
!*
!*
MPTEMP 
MPTEMP,1,0
MPDATA,EX,2,,ELAST  
MPDATA,PRXY,2,,NU   
MPTEMP 
MPTEMP,1,0  
MPDATA,DENS,2,,rho  
MPTEMP 
MPTEMP,1,0  
MPDATA,DMPR,2,,D2
!*
!* create node 1-3
N,1,0,0
N,2,2.5,0
N,3,2.5,0
!*
!* create local coordinate system
LOCAL,11,0,2.5,0,0,66.024, , ,1,1,  
CSYS,11
!*
!* create node 4-8
N,4,2.5,0
N,5,5.5,0
N,6,8.5,0
N,7,11
N,8,11
*AFUN,DEG  
!*
!* create local coordinate system
LOCAL,12,0,2.5+11*cos(66.024),11*sin(66.024),0,-73.21,,,1,1
CSYS,12
!*
!* create node 9-12
N,9,2.5,0
N,10,5.25,0
N,11,8.0,0
N,12,10.5
!*
!* create beam elements
REAL,1
MAT,1
E,1,2
E,3,4
E,4,5
E,5,6
E,6,7
MAT,2
E,8,9
E,9,10
E,10,11
E,11,12
!*
!* revolute joints
TYPE,2
REAL,2
E,2,3
E,7,8
!*
*AFUN,RAD
!*
FINISH  
/SOL
!*  
!* transient analysis
ANTYPE,4
!*  
TRNOPT,FULL 
LUMPM,0 
!* nonlinear transient
NLGEOM,1
!*
!* set kinematic constraints
D,1,UX,0
D,1,UY,0
D,1,ROTZ,-0.04188
D,12,UX,0
D,12,UY,0
D,ALL,UZ,0
ACEL,209.4,324.7,0
!* solve first loadstep
NSUBST,1,0,0
OUTRES,ALL,ALL  
NEQIT,50
TIME,0.001
SOLVE
!*
!* new kinematic constraint
D,1,ROTZ,-25.13274
!*
!* solve second loadstep
NSUBST,99,0,0   
KBC,0.0 
NEQIT,30
TIME,0.6
TIMINT,1
SOLVE
FINISH  
!
!* use timehistory postproc
/POST26